outside says ?pk:text ?pa:number ?pz:number has child ?ck:text ?ca:number ?cz:number with text ?c:text
=

child ?ck:text ?ca:number ?cz:number of ?pk:text ?pa:number ?pz:number has text ?c:text
+
outside says ?pk ?pa ?pz has child ?ck ?ca ?cz with text ?c
+
?ck is found in ?pk by capture ?n of ?re
child ?pk ?pa ?pz of _ _ _ has text ?p
capture ?n of result _ of ?p searched by ?re is at ?ra to ?rz
?ca = ?pa + ?ra
?cz = ?pa + ?rz
the text at ?ra to ?rz in ?p is ?c

?child:text is found in ?parent:text by capture ?n:number of ?regex:text
=
"view" "program" 0 "(.+\n?)+"
"head" "view" 0 "^.*"
"insert" "view" 1 "\n\+((\n[^\+-=].*)+)"
"remove" "view" 1 "\n-((\n[^\+-=].*)+)"
"input" "view" 1 "\n=(.*(\n[^\+-=].*)+)"
"variable with kind" "head" 2 "(^|\s)(\?[:alnum:]*:[:alnum:]*)"
"kind" "variable with kind" 1 ":([:alnum:]*)"
"variable" "variable with kind" 1 "\?([:alnum:]*)"
"clause" "insert" 1 "\n(.*)"
"clause" "remove" 1 "\n(.*)"
"negation" "clause" 0 "^! "
"clause body" "clause" 2 "^(! )?(.*)"
"binding" "clause body" 2 "(^|\s)(_\S*|\?[:alnum:]*|#[:digit:]+|-?[:digit:]+(\.[:digit]+)?|\x22(\\\x22|[^\x22])*\x22)"
"unbound" "binding" 0 "^_\S*$"
"variable" "binding" 1 "^\?([:alnum:]*)$"
"id" "binding" 0 "^#[:digit:]+$"
"number" "binding" 0 "^-?[:digit:]+(\.[:digit]+)?$"
"text" "binding" 0 "^\x22(\\\x22|[^\x22])*\x22$"
"import" "input" 0 "^.*"
"filename" "import" 1 "^\s*(\S*)"
"cols" "import" 1 "^\s*\S*(.*)"
"col" "cols" 1 "\s*(\S*)"
"row" "input" 1 "\n(.*)"
"id" "row" 2 "(^|\s)(#[:digit:]+)"
"number" "row" 2 "(^|\s)(-?[:digit:]+(\.[:digit]+)?)"
"text" "row" 2 "(^|\s)(\x22(\\\x22|[^\x22])*\x22)"

head ?va:number ?vz:number is named ?n:text
+
child "head" ?va ?vz of _ _ _ has text ?v
"variable with kind" is found in "head" by capture _ of ?re
?v with ?re replaced by "$1_" is ?n

clause ?va:number ?vz:number is named ?n:text
+
child "clause body" _ _ of "clause" ?va ?vz has text ?v
"binding" is found in "clause" by capture _ of ?re
?v with ?re replaced by "$1_" is ?n

view ?n:text is primitive
=
"_ = _ + _"
"_ = sum(_)"
"row _"
"_ < _"
"_ <- _"
"_ <<- _"
"min"
"result _ of _ split by _ is at _ to _ breaking at _"
"result _ of _ searched by _ is at _ to _"
"capture _ of result _ of _ searched by _ is at _ to _"
"_ with _ replaced by _ is _"
"the text at _ to _ in _ is _"
"_ has length _"

clause ?va:number ?vz:number is primitive
+
clause ?va ?vz is named ?n
view ?n is primitive

clause ?va:number ?vz:number is negated
+
child "negation" _ _ of "clause" ?va ?vz has text _

clause ?va:number ?vz:number is finite
+
clause ?va ?vz is named _
! clause ?va ?vz is primitive
! clause ?va ?vz is negated