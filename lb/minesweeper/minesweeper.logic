block(`minesweeper) {
  alias_all(`ui:dom),
  
  clauses(`{
  state[] = s ->
    string(s).
    
  num_x[] = c ->
    int(c).
    
  num_y[] = c ->
    int(c).
  
  cell(id; x,y) ->
    string(id), int(x), int(y).
  
  mine(x,y) ->
    int(x), int(y).
  
  mine_count[x,y] = c ->
    int(x), int(y), int(c).
  
  cleared(x,y) ->
    int(x), int(y).
    
  num_cleared[] = c ->
    int(c).
    
  num_mines[] = c ->
    int(c).
    
  mine_x(i; x) -> 
    int(i), int(x).
    
  mine_x(i; x) <- 
    series<< x = rnd_uniform_int<1, 1000000000, 321>[i] >>
    int:range(1, num_mines[], 1, i).
    
  mine_y(i; y) -> 
    int(i), int(y).
      
  mine_y(i; y) <- 
    series<< y = rnd_uniform_int<1, 1000000000, 123>[i] >>
    int:range(1, num_mines[], 1, i).
    
  mine(x,y) <-
    int:range(1, num_mines[], 1, i),
    mine_x(i;xx),
    mine_y(i;yy),
    x = 1 + int:mod[xx, num_x[]],
    y = 1 + int:mod[yy, num_y[]].
  
  cell(id;x,y) <-
    int:range(1,num_x[],1,x),
    int:range(1,num_y[],1,y),
    id = "cell-" + int:string:convert[x] + "-" + int:string:convert[y].
    
  neighbouring_mine(x,y,nx,ny) <-
    cell(_;x,y),
    int:range(x-1,x+1,1,nx),
    int:range(y-1,y+1,1,ny),
    boolean:or(int:ne_3[nx,x],int:ne_3[ny,y], true),
    mine(nx,ny).
  
  mine_count[x,y] = c <-
    agg<< c=count() >>
    cell(_;x,y),
    neighbouring_mine(x,y,nx,ny).
    
  mine_count[x,y] = 0 <-
    cell(_;x,y),
    !neighbouring_mine(x,y,_,_).
    
  +cleared(x,y) <-
    +click(id),
    cell(id;x,y).
    
  +cleared(nx,ny) <-
    +cleared(x,y),
    mine_count(x,y;0),
    int:range(x-1,x+1,1,nx),
    int:range(y-1,y+1,1,ny),
    boolean:or(int:ne_3[nx,x],int:ne_3[ny,y], true),
    cell(_;nx,ny).
    
  num_cleared[] = c <-
    agg<< c=count() >>
    cleared(x,y).
    
  ^state[] = "game lost" <-
    +click(id),
    cell(id;x,y),
    mine(x,y).
    
  ^state[] = "game won" <-
    +click(id),
    cell(id;x,y),
    !mine(x,y),
    !state@prev[] = "game lost",
    num_cleared[] + num_mines[] = num_x[] * num_y[].
    
  node("grid"; "root", 1, "div"),
  style("grid", "display"; "flex"),
  style("grid", "flex-direction"; "column") <- .
  
  node(id; "grid", x, "div"),
  style(id, "display"; "flex"),
  style(id, "flex-direction"; "row") <-
    cell(_;x,_),
    id = "row-" + int:string:convert[x].
    
  node(id; parent, y, "button"),
  style(id, "width"; "2em"),
  style(id, "height"; "2em"),
  onclick(id) <-
    cell(id;x,y),
    parent = "row-" +  int:string:convert[x].
  
  text[id] = "_" <-
    cell(id;x,y),
    state[] = "game in progress",
    cleared(x,y),
    mine_count[x,y] = 0.
    
  text[id] = int:string:convert[c] <-
    cell(id;x,y),
    state[] = "game in progress",
    cleared(x,y),
    mine_count[x,y] = c,
    c != 0.
    
  text[id] = "M" <-
    cell(id;x,y),
    state[] = "game won",
    mine(x,y).
    
  text[id] = "*" <-
    cell(id;x,y),
    state[] = "game lost",
    mine(x,y).
    
  text[id] = int:string:convert[c] <-
    cell(id;x,y),
    state[] != "game in progress",
    !mine(x,y),
    mine_count[x,y] = c.
  })
} <-- .
