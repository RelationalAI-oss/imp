block(`flappy) {
  alias(`ui:dom, `dom),
  
  clauses(`{
  
    // state
  
    game_running() ->.
    
    player_x[] = x ->
      float(x).

    player_y[] = y -> 
      float(y).
      
    player_dy[] = dy ->
      float(dy).
      
    seed[] = seed ->
      int(seed).
      
    // event handlers
    
    +game_running(),
    ^player_x[] = 20f,
    ^player_y[] = 45f,
    ^player_dy[] = 0f <-
      !game_running@prev(),
      +dom:click(_).
    
    //-game_running() <-
    //  +dom:frame(),
    //  game_running@prev(),
    //  collision().
      
    ^player_x[] = player_x@prev[] + 1f,
    ^player_y[] = player_y@prev[] + player_dy@prev[] <-
      +dom:frame(),
      game_running@prev().
    
    ^player_dy[] = player_dy@prev[] + 0.1f <-
      +dom:frame(),
      game_running@prev(),
      !+dom:click(_).
     
    ^player_dy[] = player_dy@prev[] - 1f <-
      +dom:frame(),
      game_running@prev(),
      +dom:click(_).
      
    // computed
    
    viewport_width[] = 100f.
    viewport_height[] = 100f.
    floor_height[] = 85f.
    
    player_width[] = 10f.
    player_height[] = 10f.
    
    obstacle_width[] = 20f.
    obstacle_height[] = 20f.
    
    obstacle_spacing[] = 200f.
      
    obstacle(x, y) ->
      float(x), float(y).
      
    obstacle(x, y) <-
      series<< y = rnd_uniform_real<5f, 35f, seed>[x] >>
      seed[] = seed,
      float:range(player_x[] - obstacle_width[], player_x[] + viewport_width[] + obstacle_width[], obstacle_spacing[], x).
      
    collision() <-
      !(floor_height[] <= player_y[] <= viewport_height[]) .
      
    collision() <- 
      obstacle(x,y),
      x - obstacle_width[] <= player_x[] <= x + obstacle_width[],
      !(y - obstacle_height[] <= player_y[] <= y + obstacle_height[]).
      
    // ui
    
    dom:node(id),
    dom:parent[id] = "root",
    dom:position[id] = 0,
    dom:tag[id] = "svg:svg",
    dom:attribute[id, "width"] = "480",
    dom:attribute[id, "viewBox"] = "10 0 80 100",
    dom:listen_to(id, "click") <-
      id = "viewport".
      
    dom:node(id),
    dom:parent[id] = "viewport",
    dom:position[id] = 0,
    dom:tag[id] = "svg:rect",
    dom:attribute[id, "x"] = "0",
    dom:attribute[id, "y"] = "0",
    dom:attribute[id, "width"] = "100",
    dom:attribute[id, "height"] = "53",
    dom:attribute[id, "fill"] = "rgb(112, 197, 206)" <-
      id = "sky".
      
    dom:node(id),
    dom:parent[id] = "viewport",
    dom:position[id] = 1,
    dom:tag[id] = "svg:image",
    dom:attribute[id, "x"] = "0",
    dom:attribute[id, "y"] = "52",
    dom:attribute[id, "width"] = "100",
    dom:attribute[id, "height"] = "43",
    dom:attribute[id, "preserveAspectRatio"] = "xMinYMin slice",
    dom:attribute[id, "xlink:href"] = "https://cdn.rawgit.com/bhauman/flappy-bird-demo/master/resources/public/imgs/background.png" <-
      id = "background".
      
    dom:node(id),
    dom:parent[id] = "viewport",
    dom:position[id] = 2,
    dom:tag[id] = "svg:rect",
    dom:attribute[id, "x"] = "0",
    dom:attribute[id, "y"] = "95",
    dom:attribute[id, "width"] = "100",
    dom:attribute[id, "height"] = "5",
    dom:attribute[id, "fill"] = "rgb(222, 216, 149)" <- 
      id = "ground".
    
    dom:node(id),
    dom:parent[id] = "viewport",
    dom:position[id] = 3,
    dom:tag[id] = "svg:text",
    dom:attribute[id, "x"] = "50",
    dom:attribute[id, "y"] = "45",
    dom:attribute[id, "text-anchor"] = "middle",
    dom:attribute[id, "font-size"] = "6",
    dom:text[id] = "Click the screen to begin!" <-
      id = "start",
      !game_running().
      
    dom:node(id),
    dom:parent[id] = "viewport",
    dom:position[id] = 3,
    dom:tag[id] = "svg:image",
    dom:attribute[id, "x"] = float:string:convert[20f],
    dom:attribute[id, "y"] = float:string:convert[player_y[]],
    dom:attribute[id, "width"] = float:string:convert[player_width[]],
    dom:attribute[id, "height"] = float:string:convert[player_height[]],
    dom:attribute[id, "xlink:href"] = "https://cdn.rawgit.com/bhauman/flappy-bird-demo/master/resources/public/imgs/flappy-base.png",
    dom:text[id] = "Click the screen to begin!" <-
      id = "flappy",
      game_running().
  })
} <-- .
