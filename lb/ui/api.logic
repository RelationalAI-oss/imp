block(`api) {
  clauses(`{

    answer[req] = resp -> ui:Request(req), ui:Response(resp).
    lang:constructor(`answer).
    lang:pulse(`answer).
    
    +ui:dom:click(id) <-
      +ui:Request(req),
      +ui:Request_click(req, click),
      +ui:Click_id(click, id).
      
    +ui:dom:key_down(id; key, text) <-
      +ui:Request(req),
      +ui:Request_key_down(req, key_down),
      +ui:KeyDown_id(key_down, id),
      +ui:KeyDown_key(key_down, key),
      +ui:KeyDown_text(key_down, text).
        
    removed(l, id) ->
      int(l), string(id).
    lang:pulse(`removed).
        
    +removed(l, id) <-
      +ui:Request(_),
      -ui:dom:node(id; _, _, _),
      ui:dom:level@prev(id; l).
      
    sorted_removed(i; l, id) ->
      int(i), int(l), string(id).
    lang:pulse(`sorted_removed).
    
    +sorted_removed(i; l, id) <-
      seq<< i = (l, id) >>
      +removed(l, id).
      
    inserted(l, parent, ix, id, tag) ->
      int(l), string(parent), int(ix), string(id), string(tag).
    lang:pulse(`inserted).
      
    +inserted(l, parent, ix, id, tag) <-
      +ui:Request(_),
      ^ui:dom:node(id; parent, ix, tag),
      ui:dom:level(id; l). 
      
    +inserted(l, parent, ix, id, tag) <-
      +ui:Request_first_render(_, true),
      ui:dom:node(id; parent, ix, tag),
      ui:dom:level(id; l). 
      
    sorted_inserted(i; l, parent, ix, id, tag) ->
      int(i), int(l), string(parent), int(ix), string(id), string(tag).
    lang:pulse(`sorted_inserted).
    
    +sorted_inserted(i; l, parent, ix, id, tag) <-
      seq<< i = (l, parent, ix, id, tag) >>
      +inserted(l, parent, ix, id, tag).
      
    style(id, key; val) ->
      string(id), string(key), string(val).
    lang:pulse(`style).
    
    +style(id, key; val) <-
      +ui:Request(_),
      ^ui:dom:style(id, key; val).
      
    +style(id, key; val) <-
      +ui:Request_first_render(_, true),
      ui:dom:style(id, key; val).
      
    sorted_style(i; id, key, val) ->
      int(i), string(id), string(key), string(val).
    lang:pulse(`sorted_style).
      
    +sorted_style(i; id, key, val) <-
      seq<< i = (id, key, val) >>
      +style(id, key; val).
      
    text(id; text) ->
      string(id), string(text).
    lang:pulse(`text).
      
    +text(id, text) <-
      +ui:Request(_),
      ^ui:dom:text(id; text).
      
    +text(id, text) <-
      +ui:Request_first_render(_, true),
      ui:dom:text(id; text).
      
    sorted_text(i; id, text) ->
      int(i), string(id), string(text).
    lang:pulse(`sorted_text).
    
    +sorted_text(i; id, text) <-
      seq<< i = (id, text) >>
      +text(id, text).
      
    on_click(id) ->
      string(id).
    lang:pulse(`on_click).
      
    +on_click(id) <-
      +ui:Request(_),
      +ui:dom:on_click(id).
      
    +on_click(id) <-
      +ui:Request_first_render(_, true),
      ui:dom:on_click(id).
      
    sorted_on_click(i; id) ->
      int(i), string(id).
    lang:pulse(`sorted_on_click).
      
    +sorted_on_click(i; id) <-
      seq<< i = (id) >>
      +on_click(id).
      
    on_key_down(id) ->
      string(id).
    lang:pulse(`on_key_down).
    
    +on_key_down(id) <-
      +ui:Request(_),
      +ui:dom:on_key_down(id).
      
    +on_key_down(id) <-
      +ui:Request_first_render(_, true),
      ui:dom:on_key_down(id).
      
    sorted_on_key_down(i; id) ->
      int(i), string(id).
    lang:pulse(`sorted_on_key_down).
      
    +sorted_on_key_down(i; id) <-
      seq<< i = (id) >>
      +ui:dom:on_key_down(id).

    +ui:Response(resp),
    +answer[req] = resp <-
      +ui:Request(req).

   +ui:Response_removed[resp, i] = id <-
     +answer[_] = resp,
     +sorted_removed(i; _, id).
     
   +ui:Response_parent[resp, i] = parent <-
     +answer[_] = resp,
     +sorted_inserted(i; _, parent, _, _, _).
     
   +ui:Response_ix[resp, i] = ix <-
     +answer[_] = resp,
     +sorted_inserted(i; _, _, ix, _, _).
       
   +ui:Response_id[resp, i] = id <-
     +answer[_] = resp,
     +sorted_inserted(i; _, _, _, id, _).
     
   +ui:Response_tag[resp, i] = tag <-
     +answer[_] = resp,
     +sorted_inserted(i; _, _, _, _, tag).
     
   +ui:Response_text_id[resp, i] = id <-
     +answer[_] = resp,
     +sorted_text(i; id, _).
     
   +ui:Response_text[resp, i] = text <-
     +answer[_] = resp,
     +sorted_text(i; _, text).
     
   +ui:Response_style_id[resp, i] = id <-
     +answer[_] = resp,
     +sorted_style(i; id, _, _).
     
   +ui:Response_style_key[resp, i] = key <-
     +answer[_] = resp,
     +sorted_style(i; _, key, _).
  
   +ui:Response_style_val[resp, i] = val <-
     +answer[_] = resp,
     +sorted_style(i; _, _, val).
     
   +ui:Response_on_click[resp, i] = id <-
     +answer[_] = resp,
     +sorted_on_click(i; id).
     
   +ui:Response_on_key_down[resp, i] = id <-
     +answer[_] = resp,
     +sorted_on_key_down(i; id).
  })
} <-- .
